import static net.modificationstation.stationapi.gradle.SubprojectHelpers.addDependencyXML

plugins {
    id "maven-publish"
    id "fabric-loom" version "1.14.10"
    id "babric-loom-extension" version "1.14.1"
    id "com.modrinth.minotaur" version "2.+"
}

version = property("mod_version")
group = property("maven_group")

base {
    archivesName = project.archives_base_name
}

allprojects {
    apply plugin: "maven-publish"
    apply plugin: "fabric-loom"
    apply plugin: "babric-loom-extension"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withSourcesJar()
    }

    babric {
        disablePomOverride.set(true)
    }
    
    loom {
        mixin {
            useLegacyMixinAp = false
        }
    }

    repositories {
        // Used for Babric Loom Extensions
        maven {
            name = "Babric"
            url = "https://maven.glass-launcher.net/babric"
        }
        
        // NyaRepo
        maven {
            name = "NyaRepo"
            url = "https://maven.fildand.cz/releases"
        }

        // Used for many dependencies
        maven {
            name = "Glass Releases"
            url = "https://maven.glass-launcher.net/releases"
        }

        // Used for Snapshots
        maven {
            name = "Glass Snapshots"
            url = "https://maven.glass-launcher.net/snapshots"
        }


        // Used for a StationAPI dependency.
        maven {
            name = "Froge Maven"
            url = "https://maven.minecraftforge.net/"
        }

        // Used for projects that do not have a maven repository, but do have a GitHub repository with working build scripts.
        maven {
            name = "Jitpack"
            url = "https://jitpack.io"
        }

        // Modrinth Maven
        exclusiveContent {
            forRepository {
                //noinspection ForeignDelegate
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }
            filter {
                includeGroup "maven.modrinth"
            }
        }

        mavenCentral()
    }

    configurations {
        transitiveImplementation
        implementation.extendsFrom configurations.transitiveImplementation
    }

    dependencies {
        minecraft("com.mojang:minecraft:${project.minecraft_version}")
        mappings("net.glasslauncher:biny:${project.yarn_mappings}:v2")
        modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

        // Gradle 8 used to bundle this, Gradle 9 no longer does
        implementation("org.codehaus.groovy:groovy-xml:3.0.21")
        implementation("org.slf4j:slf4j-api:1.8.0-beta4")
        implementation("org.apache.logging.log4j:log4j-slf4j18-impl:2.17.2")
        implementation("me.carleslc:Simple-Yaml:1.8.4")

        modImplementation ("net.modificationstation:StationAPI:${project.stapi_version}") {
            exclude group: "babric", module: "fabric-loader"
        }

        modImplementation("net.danygames2014:modmenu:${project.modmenu_version}") {
            transitive = false
        }

        modImplementation("net.glasslauncher.mods:glass-networking:${project.glass_networking_version}") {
            transitive = false
        }

        modImplementation("net.glasslauncher.mods:GlassConfigAPI:${project.gcapi_version}") {
            transitive = false
        }

        modImplementation("net.glasslauncher.mods:AlwaysMoreItems:${project.alwaysmoreitems_version}") {
            transitive = false
        }

        modImplementation("maven.modrinth:retrocommands:${project.retrocommands_version}-b1.7.3") {
            transitive = false
        }

        modImplementation("net.danygames2014:spawneggs:${project.spawneggs_version}") {
            transitive = false
        }

        modImplementation("maven.modrinth:bh-creative:${project.bhcreative_version}-b1.7.3") {
            transitive = false
        }
        
        modImplementation("net.danygames2014:WhatsThis:${project.whatsthis_version}") {
            transitive = false
        }

        modRuntimeOnly("net.danygames2014:UniWrench:${project.uniwrench_version}") {
            transitive = false
        }
        
        modRuntimeOnly("net.danygames2014:DebugUtilities:${project.debugutilities_version}") {
            transitive = false
        }

        modRuntimeOnly("maven.modrinth:retroauth:${project.retroauth_version}-b1.7.3") {
            transitive = false
        }

        modRuntimeOnly("maven.modrinth:unitweaks:${project.unitweaks_version}-b1.7.3") {
            transitive = false
        }

        modRuntimeOnly("maven.modrinth:fast-stapi-intro:2.0.0-b1.7.3") {
            transitive = false
        }
    }

    tasks.named("processResources", ProcessResources) {
        def ver = project.properties["mod_version"]

        if (project.properties["override_version"] != null) {
            ver = "${project.properties['mod_version']}+${project.properties['override_version']}"
        }

        inputs.property("version", ver)

        filesMatching("fabric.mod.json") {
            expand(version: ver)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
        it.options.encoding = "UTF-8"
    }
    
    // Disables the generation of the .module file
    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    // Gradle 9 fails the build if there are no tests, but we are using the test package for a test mod
    tasks.withType(Test).configureEach {
        failOnNoDiscoveredTests = false
    }
    
    tasks.named("jar", Jar) {
        from("LICENSE") {
            rename { "${it}_${project.properties['archivesBaseName']}" }
        }
    }

    // Maven Publishing
    publishing {
        repositories {
            mavenLocal()

            // Glass Maven
            if (project.publish_to_glass_maven.equalsIgnoreCase("true")) {
                if (project.hasProperty("calmilamsy_secret_publishing_cave_username")) {
                    maven {
                        url = "https://maven.glass-launcher.net/releases"
                        credentials {
                            username = "${project.calmilamsy_secret_publishing_cave_username}"
                            password = "${project.calmilamsy_secret_publishing_cave_password}"
                        }
                    }
                }
            }

            // NyaRepo
            if (project.hasProperty("nyarepo_publishing_cave_username")) {
                maven {
                    url = "https://maven.fildand.cz/releases"
                    credentials {
                        username = "${project.nyarepo_publishing_cave_username}"
                        password = "${project.nyarepo_publishing_cave_password}"
                    }
                }
            }
        }

        publications {
            create("mavenJava", MavenPublication) {
                //noinspection GrUnresolvedAccess
                artifactId = base.archivesName.get()
                //noinspection GrUnresolvedAccess, GroovyAssignabilityCheck
                from components.java
            }
        }
    }
}

subprojects {
    group = project.properties['maven_group'] + ".NyaLib.submodule"

    configurations {
        out {
            canBeConsumed = true
            canBeResolved = false
        }
        dev {
            canBeConsumed = true
            canBeResolved = false
        }
        test {
            canBeConsumed = true
            canBeResolved = false
        }
    }

    tasks.register('testJar', Jar) {
        from sourceSets.test.output
        archiveClassifier.convention('test')
        archiveClassifier.set('test')
    }

    artifacts {
        add("out", tasks.getByName("remapJar"))
        add("dev", tasks.getByName("jar"))
        add("test", tasks.getByName("testJar"))
    }

    rootProject.dependencies {
        implementation project(path: ":$name", configuration: "dev")
        testImplementation project(path: ":$name", configuration: "test")
        include project(path: ":$name", configuration: "out")
    }

    publishing {
        publications {
            mavenJava {
                pom.withXml { XmlProvider xml ->
                    println("Wiping the POM file dependencies for submodule " + this.rootProject)
                    Node dependenciesNode = net.fabricmc.loom.util.GroovyXmlUtil.getOrCreateNode(xml.asNode(), "dependencies")
                    if (dependenciesNode) {
                        xml.asNode().remove(dependenciesNode)
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava {
            artifactId = project.properties['archives_base_name']

            pom.withXml { XmlProvider xml ->
                println("Wiping the POM file dependencies")
                Node dependenciesNode = net.fabricmc.loom.util.GroovyXmlUtil.getOrCreateNode(xml.asNode(), "dependencies")
                if (dependenciesNode) {
                    xml.asNode().remove(dependenciesNode)
                }
                
                project.subprojects.each { subproject ->
                    println("Adding subproject as a dependency: ${subproject.name}")

                    //noinspection GroovyAssignabilityCheck
                    addDependencyXML(xml.asNode(), "compile", subproject)
                }
            }
        }
    }
}

tasks.register('testJar', Jar) {
    from sourceSets.test.output
    archiveClassifier.convention('test')
    archiveClassifier.set('test')
}

// Modrinth Publishing
modrinth {
    if (project.hasProperty("modrinth_publishing_cave")) {
        token = project.modrinth_publishing_cave
        projectId = project.modrinth_slug
        versionNumber = project.mod_version
        versionName = "${project.modrinth_version_name} ${project.mod_version}"
        changelog = rootProject.file("CHANGELOG.md").text
        versionType = "release"
        uploadFile = remapJar
        gameVersions = ["b1.7.3"]
        loaders = ["fabric", "babric"]
        dependencies {
            required.project "stationapi"
            required.project "glass-config-api"
        }
    }
}

// Updater
tasks.register("update") {
    doLast {
        def propsFile = file("gradle.properties")

        List<String> lines = propsFile.readLines()

        updateDependency(lines, "BINY", "yarn_mappings", "https://maven.glass-launcher.net/releases/net/glasslauncher/biny")
        updateDependency(lines, "Fabric Loader", "loader_version", "https://maven.fabricmc.net/net/fabricmc/fabric-loader/")
        updateDependency(lines, "StationAPI", "stapi_version", "https://maven.glass-launcher.net/releases/net/modificationstation/StationAPI")
        updateDependency(lines, "Glass Networking", "glass_networking_version", "https://maven.glass-launcher.net/releases/net/glasslauncher/mods/glass-networking")
        updateDependency(lines, "Glass Config API", "gcapi_version", "https://maven.glass-launcher.net/releases/net/glasslauncher/mods/GlassConfigAPI")
        updateDependency(lines, "Always More Items", "alwaysmoreitems_version", "https://maven.glass-launcher.net/releases/net/glasslauncher/mods/AlwaysMoreItems")
        updateDependency(lines, "ModMenu", "modmenu_version", "https://maven.glass-launcher.net/releases/net/danygames2014/modmenu")
        updateDependency(lines, "BH Creative", "bhcreative_version", "https://api.modrinth.com/maven/maven/modrinth/bh-creative")
        updateDependency(lines, "Spawn Eggs", "spawneggs_version", "https://maven.glass-launcher.net/releases/net/danygames2014/spawneggs")
        updateDependency(lines, "Retro Commands", "retrocommands_version", "https://api.modrinth.com/maven/maven/modrinth/retrocommands")
        updateDependency(lines, "Accessory API", "accessoryapi_version", "https://api.modrinth.com/maven/maven/modrinth/accessory-api")
        updateDependency(lines, "RetroAuth", "retroauth_version", "https://api.modrinth.com/maven/maven/modrinth/retroauth")
        updateDependency(lines, "UniTweaks", "unitweaks_version", "https://api.modrinth.com/maven/maven/modrinth/unitweaks")
        updateDependency(lines, "Whats This", "whatsthis_version", "https://maven.glass-launcher.net/releases/net/danygames2014/WhatsThis")
        updateDependency(lines, "NyaLib", "nyalib_version", "https://maven.glass-launcher.net/releases/net/danygames2014/NyaLib")
        updateDependency(lines, "UniWrench", "uniwrench_version", "https://maven.glass-launcher.net/releases/net/danygames2014/UniWrench")
        updateDependency(lines, "DebugUtilities", "debugutilities_version", "https://maven.glass-launcher.net/releases/net/danygames2014/DebugUtilities")

        propsFile.text = lines.join(System.lineSeparator())
        println("Finished updating dependencies, please reload your Gradle Project")
    }
}

def updateDependency(List<String> lines, String name, String property, String artifactUrl) {
    println("Checking updates for " + name)

    if (!project.hasProperty(property)) {
        println("Project does not have property " + property)
        return
    }

    String latestVersion = fetchLatestVersion(artifactUrl)

    if (latestVersion == null) {
        println("Error while fetching latest version")
        return
    }

    for (int i = 0; i < lines.size(); i++) {
        if (lines[i].contains(property)) {
            String[] split = lines[i].split("=")
            String currentVersion = split[1]

            if (!currentVersion.equalsIgnoreCase(latestVersion)) {
                println("Dependency " + name + " updated. (" + currentVersion + " -> " + latestVersion + ")")
                lines[i] = split[0] + "=" + latestVersion
            } else {
                println("Dependency " + name + " is already up to date. (" + currentVersion + ")")
            }

            break
        }
    }
}

static def fetchLatestVersion(String artifactUrl) {
    String latestVersion

    try {
        String artifactMeta = new URL(artifactUrl + "/maven-metadata.xml").text
        latestVersion = artifactMeta.split("<latest>")[1].split("</latest>")[0]
    } catch (Exception e) {
        println("Error while updating dependency " + artifactUrl)
        println(e.message)
        return null
    }

    return latestVersion
}